import org.flywaydb.gradle.task.FlywayCleanTask
import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'org.flywaydb.flyway' version '6.5.6'
	id 'java'
}

group = 'net.digitallogic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'com.google.code.findbugs:jsr305:3.0.2'

	compileOnly 'org.projectlombok:lombok:1.18.16'
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.assertj:assertj-core:3.16.1'
	testCompileOnly 'org.projectlombok:lombok:1.18.16'

	annotationProcessor 'org.projectlombok:lombok:1.18.16'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

	annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
	testImplementation 'com.github.javafaker:javafaker:1.0.2'
}

flyway {
	url = 'jdbc:postgresql://localhost/project_manager'
	user = 'pm_migration_user'
	password = 'password'
	schemas = ['public']
	placeholders = [
			'sysAccountEmail': "'system_account@localhost'",
			'sysAccountPwd': "'{bcrypt}\$2y\$17\$mnQvgxTWmCsE2Pecu2zk7uKnrfeH5Qhgw6A7qR6asRWBmvH7Mg4Qu'",
			'app_user': 'pm_app_user'
	]
}

task migrateTest(type: FlywayMigrateTask) {
	group = 'flywayTest'
	url = 'jdbc:postgresql://localhost/project_manager_test'
}

task cleanTest(type: FlywayCleanTask) {
	group = 'flywayTest'
	url = 'jdbc:postgresql://localhost/project_manager_test'
}

test {
	useJUnitPlatform()
}
