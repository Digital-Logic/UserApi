import org.flywaydb.gradle.task.FlywayCleanTask
import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id 'org.springframework.boot' version '2.4.2' // '2.4.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.flywaydb.flyway' version '6.5.6'
    id 'nu.studer.jooq' version '5.2'
    id 'java'
}

def dbConfig = [
        dev : [dbUrl   : 'jdbc:postgresql://localhost/project_manager',
               app: [user: 'pm_app_user', password: 'password'],
               mirgration : [user: 'pm_migration_user', password: 'password']
        ],
        test: [dbUrl   : 'jdbc:postgresql://localhost/project_manager_test',
               app: [user: 'pm_app_user', password: 'password'],
               mirgration: [user: 'pm_migration_user', password: 'password']
        ]
]

group = 'net.digitallogic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.session:spring-session-data-redis'
    // implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'io.lettuce:lettuce-core:6.0.2.RELEASE'
    //implementation 'org.springframework.session:spring-session-jdbc'

    // Json web token
    implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
    runtime 'io.jsonwebtoken:jjwt-impl:0.11.1',
            'io.jsonwebtoken:jjwt-jackson:0.11.1'

    implementation 'org.flywaydb:flyway-core'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'cz.jirutka.rsql:rsql-parser:2.1.0'

    jooqGenerator 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'

    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
}

flyway {
    url = dbConfig.dev.dbUrl
    user = dbConfig.dev.mirgration.user
    password = dbConfig.dev.mirgration.password
    schemas = ['public']
    placeholders = [
            'sysAccountEmail': "'system_account@localhost'",
            'sysAccountPwd'  : "'{bcrypt}\$2y\$17\$mnQvgxTWmCsE2Pecu2zk7uKnrfeH5Qhgw6A7qR6asRWBmvH7Mg4Qu'",
            'app_user'       : dbConfig.dev.app.user
    ]
}

jooq {
    version = '3.13.4'

    configurations {
        main {  // name of the jOOQ configuration
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = dbConfig.dev.dbUrl
                    user = dbConfig.dev.app.user
                    password = dbConfig.dev.app.password
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                    }
                    generate {
                        deprecated = false
                        records = true
                        pojos = false
                        routines = false
                        //immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'net.digitallogic.ProjectManager'
                        directory = 'build/generated/sources/jooq/java/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

tasks.withType(org.gradle.api.tasks.testing.Test) {
    environment('TZ', 'UTC')
}

tasks.withType(JavaCompile) {
    options.verbose
    options.compilerArgs += ["-Xlint:unchecked"]
}


task migrateTest(type: FlywayMigrateTask) {
    group = 'flywayTest'
    url = dbConfig.test.dbUrl
}

task cleanTest(type: FlywayCleanTask) {
    group = 'flywayTest'
    url = dbConfig.test.dbUrl
}

test {
    useJUnitPlatform()
}
